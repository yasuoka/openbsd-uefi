/* 	$OpenBSD: ld.script,v 1.11 2003/01/31 23:42:28 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa")
OUTPUT_ARCH(hppa)
ENTRY($start)
SECTIONS
{
	. = + SIZEOF_HEADERS;
	.text :
	{
		kernel_text = . ;
		*(.text) *(.text.*)
		*(.reginfo)
		*(.init)
		*(.stub)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		. = ALIGN(4096);
		etext = ABSOLUTE(.);
	} = 0x08000240	/* nop filled */
	.rodata :
	{
		__rodata_start = .;
		*(.rodata) *(.rodata1)
		__unwind_start = .;
		*(.PARISC.unwind)
		__unwind_end = .;
		__rodata_end = .;
	} = 0
	. = 0x400000;
	__data_start = ABSOLUTE(.);

	.data  :
	{
		$global$ = . ;
		. += 4;
		*(.data)
		CONSTRUCTORS
		. = ALIGN(4096);
		edata = ABSOLUTE(.);
	} = 0
	.ctors	: { *(.ctors) }
	.dtors	: { *(.dtors) }

	__bss_start = .;
	/* .sbss	: { *(.sbss) *(.scommon) } */
	.bss :
	{
		*(.dynbss) *(.bss)
		*(COMMON) *($COMMON$)
		. = ALIGN(4096);
	}
	end = ABSOLUTE(.);
}
