/* 	$OpenBSD: ld.script,v 1.5 1999/06/22 17:46:37 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa")
OUTPUT_ARCH(hppa)
ENTRY($start)
SECTIONS
{
	/* Read-only sections, merged into text segment: */
	. = + SIZEOF_HEADERS;
	.text BLOCK(4096) :
	{
		kernel_text = . ;
		*(.text)
		*(.rodata) *(.rodata1)
		*(.reginfo)
		*(.init)
		*(.stub)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		__unwind_start = .;
		*(.PARISC.unwind)
		__unwind_end = .;
	} = 0 /* 0x08000240 nop filled, does not work */
	etext = ABSOLUTE(.);

	.data BLOCK(4096) :
	{
		$global$ = . ;
		kernel_data = . ;
		[a-ps-z]*(.data) r[a-ce-z]*(.data) *libkern.o(.data)
		rd_root.o(.data)	/* there is an overflow bug in ld */
		CONSTRUCTORS
		. = ALIGN(4096);
	} = 0
	.ctors	: { *(.ctors) }
	.dtors	: { *(.dtors) }
	edata = ABSOLUTE(.);

	__bss_start = .;
	.sbss	: { *(.sbss) *(.scommon) }
	.bss BLOCK(4096) :
	{
		*(.dynbss) *(.bss)
		*(COMMON) *($COMMON$)
		. = ALIGN(4096);
	}
	end = ABSOLUTE(.);
}
