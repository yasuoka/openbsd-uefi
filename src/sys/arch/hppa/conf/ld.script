/* 	$OpenBSD: ld.script,v 1.2 1999/02/25 16:42:42 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa")
OUTPUT_ARCH(hppa)
ENTRY(__start)
SECTIONS
{
	/* Read-only sections, merged into text segment: */
	. = + SIZEOF_HEADERS;
	.text	:
	{
		kernel_text = . ;
		*(.text)
		*($CODE$)
		*(.rodata)
		*(.rodata1)
		*(.reginfo)
		*(.init)
		*(.stub)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
	} = 0
	etext = .;

	.data	:
	{
		kernel_data = . ;
		*(.data)
		CONSTRUCTORS
	}
	.ctors	: { *(.ctors)   }
	.dtors	: { *(.dtors)   }
	edata = .;

	__bss_start = .;
	.sbss	: { *(.sbss) *(.scommon) }
	.bss	:
	{
		*(.dynbss)
		*(.bss)
		*(COMMON)
		*($COMMON$)
	}
	end = .;
}
